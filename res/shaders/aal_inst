// RenAAL - "glowing" anti-aliased lines

// ==================
#vert
#version 330 core

uniform mat4 proj;
uniform vec4 pr;
uniform float scrmul;

layout(location = 0) in vec4 data;
layout(location = 1) in vec3 norm_in;

out vec3 norm;
out float wpar;
out float endk;

void main()
{
	norm = norm_in;
	wpar = data.z * scrmul;
	endk = data.w * scrmul;

	mat2 rot = mat2(pr.z, -pr.w, pr.w, pr.z);
	gl_Position = proj * vec4(rot * data.xy + pr.xy, 0, 1);
}

#end
// ==================
#frag
#version 330 core

uniform sampler2D tex;
uniform vec4 clr;

in vec3 norm;
in float wpar;
in float endk;
out vec4 res;

void main()
{
	float a = wpar * (1 - length(norm.xy));
	float b = endk * (1 - abs(norm.z));
	a *= clamp(b, 0, 1);
	a = texture(tex, vec2(a, 0)).r;
	res = clr * a;
}

#end
